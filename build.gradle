/*
 * Copyright 2016 Todd Kulesza <todd@dropline.net>.
 *
 * This file is part of TivoLibre. TivoLibre is derived from
 * TivoDecode 0.4.4 by Jeremy Drake. See the LICENSE-TivoDecode
 * file for the licensing terms for TivoDecode.
 *
 * TivoLibre is free software: you can redistribute it and/or modify
 * it under the terms of the GNU General Public License as published by
 * the Free Software Foundation, either version 3 of the License, or
 * (at your option) any later version.
 *
 * TivoLibre is distributed in the hope that it will be useful,
 * but WITHOUT ANY WARRANTY; without even the implied warranty of
 * MERCHANTABILITY or FITNESS FOR A PARTICULAR PURPOSE.  See the
 * GNU General Public License for more details.
 *
 * You should have received a copy of the GNU General Public License
 * along with TivoLibre.  If not, see <http://www.gnu.org/licenses/>.
 *
 */

apply plugin: 'java'
apply plugin: 'maven'
apply plugin: 'signing'

group = 'net.straylightlabs'
archivesBaseName = 'tivo-libre'
version = '0.7.4'

repositories {
    mavenCentral()
}

dependencies {
    testCompile 'junit:junit:4.12'
    compile 'commons-cli:commons-cli:1.3.1'
    compile 'commons-codec:commons-codec:1.10'
    compile group: 'org.slf4j', name: 'slf4j-api', version: '1.7.21'
    compile group: 'ch.qos.logback', name: 'logback-classic', version: '1.1.7'
}

task javadocJar(type: Jar) {
    classifier = 'javadoc'
    from javadoc
}

task sourcesJar(type: Jar) {
    classifier = 'sources'
    from sourceSets.main.allSource
}

jar {
    baseName 'tivo-libre'
}

task appJar(type: Jar) {
    archiveName = 'TivoDecoder.jar'
    from('src/main/app-resources') {
        include '*.xml'
    }
    manifest {
        attributes "Main-Class": "net.straylightlabs.tivolibre.DecoderApp"
    }
    from {
        configurations.compile.collect { it.isDirectory() ? it : zipTree(it) }
    }
    with jar
}

test {
    println "Running tests on input file '$testFile'."
    println "Using MAK '$mak'"
    systemProperties['mak'] = mak
    systemProperties['testFile'] = testFile
    systemProperties['outFile'] = outFile
}

artifacts {
    archives javadocJar, sourcesJar
}

signing {
    sign configurations.archives
}

uploadArchives {
    repositories {
        mavenDeployer {
            beforeDeployment { MavenDeployment deployment -> signing.signPom(deployment) }

            repository(url: "https://oss.sonatype.org/service/local/staging/deploy/maven2/") {
                authentication(userName: ossrhUsername, password: ossrhPassword)
            }

            snapshotRepository(url: "https://oss.sonatype.org/content/repositories/snapshots/") {
                authentication(userName: ossrhUsername, password: ossrhPassword)
            }

            pom.project {
                name 'TivoLibre'
                packaging 'jar'
                artifactId 'tivo-libre'
                description 'TivoLibre is a Java library for decoding TiVo files to standard MPEG files'
                url 'https://github.com/fflewddur/tivolibre'

                scm {
                    connection 'scm:git:https://github.com/fflewddur/tivolibre.git'
                    developerConnection 'scm:git:https://github.com/fflewddur/tivolibre.git'
                    url 'https://github.com/fflewddur/tivolibre.git'
                }

                licenses {
                    license {
                        name 'GNU General Public License (GPL)'
                        url 'http://www.gnu.org/licenses/gpl.txt'
                    }
                }

                developers {
                    developer {
                        id 'fflewddur'
                        name 'Todd Kulesza'
                        email 'todd@dropline.net'
                    }
                }
            }
        }
    }
}
